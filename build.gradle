plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    maven { url 'https://maven.terraformersmc.com/releases/' }
    mavenCentral()
}

base {
    archivesName = "${mod_id}-${minecraft_version}"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'fabric-loom'

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings "net.fabricmc:yarn:${minecraft_version}+${yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}+${minecraft_version}"
    }

    processResources {
        filesMatching('fabric.mod.json') { expand project.properties }
    }
}

subprojects {
    version = module_version

    base {
        archivesName = "${project.name}-${minecraft_version}"
    }
}

dependencies {
    modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"

    subprojects.each {
        api project(path: "${it.path}", configuration: "namedElements")
        include project(path: "${it.path}", configuration: "namedElements")
    }
}

