plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'maven-publish'
}

repositories {
    maven { url 'https://maven.terraformersmc.com/releases/' }
    mavenCentral()
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'fabric-loom'

    base {
        archivesName = "${project.name}-${mod_version}"
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings "net.fabricmc:yarn:${minecraft_version}+${yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}+${minecraft_version}"
    }

    processResources {
        filesMatching('fabric.mod.json') { expand project.properties }
    }
}

dependencies {
    modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"

    subprojects.each {
        api project(path: "${it.path}", configuration: "namedElements")
        include project(path: "${it.path}", configuration: "namedElements")
    }
}

subprojects {
    dependencies {
        if (project.name != "obscure-base") modImplementation project(path: ":obscure-base", configuration: "namedElements")
    }
}

version = project.mod_version
group = project.maven_group

publishing {

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

}
